; Z-Spectrum
; Presaturation with the decoupler channel
;
; Originally modified by Wen Ling based on zgpr
; Updated by Jae-Seung Lee (5/19/2008)
; Updated by S. Chandrashekar (9/12/2008)
;

#include <Avance.incl>

define list<frequency> fq1list = <$FQ1LIST>

"d11=30m"
"d12=10u"
"cnst21=0"

; Comment out one of the following two lines
;#define ONED
#define TWOD

1  ze
2  d1
   (d12 pl9 fq1list):f1
   d2 cw:f1 ph29
   4u do:f1
   (d12 pl1 fq=cnst21):f1	
   p1 ph1
   go=2 ph31
#ifdef ONED
    d11 wr #0
#else
    d11 wr #0 if #0 zd
#endif

#ifdef TWOD
    d11 fq1list.inc
  lo to 2 times td1
#endif

exit


ph1 =0 2 1 3
ph29=0 0 0 0
ph31=0 2 1 3

;d1: relaxation delay
;d2: delay for presaturation
;pl1: f1 channel - power level for pulse (default)
;p1 : f1 channel - 90 degree high power pulse
;pl9: f1 channel, decoupler - power level for the presautration pulse
;d12: delay for power switching
;fq1list: a list of frequency offsets
;td1: number of frequency offsets