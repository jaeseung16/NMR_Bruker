; CPMG
; T2 measurement using Carr-Purcell-Meiboom-Gill sequence
;
; By Jae-Seung Lee
; Updated on 10/11/2017
;

#include <Avance.incl>
#include <De.incl>

"p2=p1*2"
;"d11=0.5m"
"d20=aq"

1 ze
  10u st0
2 d1
  p1 ph1
  d20 * 0.5
  d11
  10u
3 p2 ph2^
  10u
  d11
  ACQ_START(ph30,ph31)
  aq DWELL_GEN:f1
  d11 eoscnp  
  10u st
  lo to 3 times td1
  5m ipp1 ipp30
  200m wr #0
  lo to 2 times ns
exit

ph1=0 2
ph2=1 3
ph30=0 2
ph31=0

;pl1 : f1 channel - power level for pulse (default)
;p1 : f1 channel -  90 degree high power pulse
;p2 : f1 channel - 180 degree high power pulse
;d1 : relaxation delay; 1-5 * T1
;td1: number of echoes
;
;echo time = d20 + d11 * 2
;d11: delay for the command 'eoscnp'
;     There is a minimum delay for the command 'eoscnp'
;     According to the TopSpin Pulse Programming manual, the minimum delay is 100 us.
;     For the machine I use, the minimum delay is a few ms.   
;     